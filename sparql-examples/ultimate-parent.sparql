PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX bods:     <http://bods.openownership.org/vocabulary/>
PREFIX bods-res: <http://bods.openownership.org/resource/>

# This query returns the ultimate parent entity for the specified target
# (openownership-register-13061757995887162872 / BESSEMER TRUST LIMITED)

SELECT ?ultimateParent ?ultimateParentName
WHERE  {
    ?ultimateParent bods:hasInterestIn+ bods-res:openownership-register-13061757995887162872 .
    ?ultimateParent foaf:name ?ultimateParentName .
    ?ultimateParent rdf:type bods:Entity .
    FILTER NOT EXISTS {
       ?s bods:hasInterestIn ?ultimateParent .
       ?s rdf:type bods:Entity .
    }
}

